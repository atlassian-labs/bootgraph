buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.41"
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group = 'com.atlassian'
// run gradle with "-Dsnapshot=true" to automatically append "-SNAPSHOT" to the version
version = '1.2.4' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

subprojects {

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'maven'

    repositories {
        jcenter()
    }

    dependencyManagement {
        dependencies {

            dependency 'org.jetbrains.kotlin:kotlin-stdlib:1.3.41'
            dependency 'org.jetbrains.kotlin:kotlin-reflect:1.3.41'
            dependency 'guru.nidi:graphviz-java:0.12.1'

            // Modules should import this in compileOnly scope to let users
            // import their own version of Spring!
            dependency 'org.springframework.boot:spring-boot-starter:2.2.2.RELEASE'
            dependency 'org.springframework.boot:spring-boot-starter-test:2.2.2.RELEASE'

            dependency 'org.junit.jupiter:junit-jupiter-api:5.5.2'
            dependency 'org.junit.jupiter:junit-jupiter-params:5.5.2'
            dependency 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

            dependency 'org.jetbrains.kotlin:kotlin-test:1.3.41'
            dependency 'org.jetbrains.kotlin:kotlin-test-junit5:1.3.41'
        }
    }

    // default dependencies for all modules
    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'

        testCompile 'org.junit.jupiter:junit-jupiter-api'
        testCompile 'org.junit.jupiter:junit-jupiter-params'
        testCompile 'org.junit.jupiter:junit-jupiter-engine'

        testCompile 'org.jetbrains.kotlin:kotlin-test'
        testCompile 'org.jetbrains.kotlin:kotlin-test-junit5'
    }

    test {
        useJUnitPlatform()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc.failOnError = false
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}